#!/usr/bin/perl

use strict;
use warnings;

#man{{{

=head1 NAME

tails-virt-notify-user

=head1 VERSION

Version X.XX

=head1 AUTHOR

Tails dev team <amnesia@boum.org>
See https://tails.boum.org/.

=cut

#}}}

use Desktop::Notify;
use English '-no_match_vars';
use IPC::System::Simple qw{capturex $EXITVAL};
use Locale::TextDomain 'tails';
use Net::DBus::Reactor;
use POSIX;

my $dont_ask_identifier = 'virt-notify-user';

### callbacks

sub action_cb {
    my $reactor = shift;
    my $action = pop;
    unless (fork) {
        if ($action eq 'more_info') {
            exec(
                '/usr/local/bin/tails-documentation',
                'doc/advanced_topics/virtualization',
                'security'
            );
        } elsif ($action eq 'hide') {
            exec(
                '/usr/local/bin/tails-ask-again',
                '--dont-ask-again',
                $dont_ask_identifier
            );
        }
    }
    $reactor->shutdown;
}

### main

my $dont_ask_status = capturex('/usr/local/bin/tails-ask-again',
                               '--timeout', '30',
                               $dont_ask_identifier);
chomp($dont_ask_status);
exit 0 if $dont_ask_status eq 'hide';

# both 0 and 1 are acceptable exit values:
#  - 0 means that we're running in a virtualized environment
#  - 1 means that we're not running in a virtualized environment
#  - anything else means there is a problem, and capturex will throw an exception
my $vm_name = capturex([0, 1], qw{/usr/bin/systemd-detect-virt --vm});
exit 0 if $EXITVAL == 1;

my @whitelist = qw(bochs kvm qemu uml virtualbox xen);

my $reactor = Net::DBus::Reactor->main;

my $notify  = Desktop::Notify->new();
$notify->action_callback(sub { action_cb($reactor, @_) });
$notify->close_callback(sub { $reactor->shutdown; });

my ($body, $summary);

chomp($vm_name);
if (grep {$_ eq $vm_name} @whitelist) {
    $summary = __("Warning: virtual machine detected!");
}
else {
    $summary = __("Warning: non-free virtual machine detected!");
}

$body = __("Both the host operating system and the virtualization software are able to monitor what you are doing in Tails. Only free software can be considered trustworthy, for both the host operating system and the virtualization software.");

my @actions = ( 'more_info', __('Learn more') );
if ($dont_ask_status ne 'unavailable') {
    push(@actions, 'hide', __('Hide warning for a month'));
}

$notify->create(summary => $summary,
                body    => $body,
                actions => \@actions,
                hints   => { 'transient' => 1, },
                timeout => 0)->show();

$reactor->run;
